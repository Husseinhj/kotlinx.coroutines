/*
 * Copyright 2016-2020 JetBrains s.r.o. Use of this source code is governed by the Apache 2.0 license.
 */
targetCompatibility = JavaVersion.VERSION_1_8

dependencies {
    jvmMainImplementation project(':kotlinx-coroutines-reactive')
    jvmTestImplementation project(':kotlinx-coroutines-reactive').kotlin.targets.jvm.compilations.test.output.allOutputs
    jvmTestImplementation "org.reactivestreams:reactive-streams-tck:$reactive_streams_version"
    jvmMainImplementation "io.reactivex.rxjava3:rxjava:$rxjava3_version"
}

kotlin {
    configure([jvm("jvm"), jvm("jvmIr")]) {
        compilations.all {
            kotlinOptions.jvmTarget = "1.8"
        }
    }
}

tasks.withType(dokka.getClass()) {
    externalDocumentationLink {
        url = new URL('http://reactivex.io/RxJava/3.x/javadoc/')
        packageListUrl = projectDir.toPath().resolve("package.list").toUri().toURL()
    }
}

task testNG(type: Test) {
    useTestNG()
    def jvmTestTask = tasks.getByName("jvmTest") as Test
    classpath = jvmTestTask.classpath
    testClassesDirs = jvmTestTask.testClassesDirs
    reports.html.destination = file("$buildDir/reports/testng")
    include '**/*ReactiveStreamTckTest.*'
    // Skip testNG when tests are filtered with --tests, otherwise it simply fails
    onlyIf {
        filter.includePatterns.isEmpty()
    }
    doFirst {
        // Classic gradle, nothing works without doFirst
        println "TestNG tests: ($includes)"
    }
}

jvmTest {
    dependsOn(testNG)
    reports.html.destination = file("$buildDir/reports/junit")
}

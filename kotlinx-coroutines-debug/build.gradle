/*
 * Copyright 2016-2020 JetBrains s.r.o. Use of this source code is governed by the Apache 2.0 license.
 */

apply plugin: "com.github.johnrengelman.shadow"

configurations {
    shadowDeps // shaded dependencies, not included into the resulting .pom file
    jvmMainCompileOnly.extendsFrom(shadowDeps)
    jvmMainRuntimeOnly.extendsFrom(shadowDeps)

    /*
     * It is possible to extend a particular configuration with shadow,
     * but in that case it changes dependency type to "runtime" and resolves it
     * (so it cannot be further modified). Otherwise, shadow just ignores all dependencies.
     */
    shadow.extendsFrom(jvmMainApi) // shadow - resulting configuration with shaded jar file
    configureKotlinJvmPlatform(shadow)
}

dependencies {
    jvmMainCompileOnly "junit:junit:$junit_version"
    shadowDeps "net.bytebuddy:byte-buddy:$byte_buddy_version"
    shadowDeps "net.bytebuddy:byte-buddy-agent:$byte_buddy_version"
    jvmMainCompileOnly "io.projectreactor.tools:blockhound:$blockhound_version"
    jvmTestImplementation "io.projectreactor.tools:blockhound:$blockhound_version"
    jvmMainApi "net.java.dev.jna:jna:$jna_version"
    jvmMainApi "net.java.dev.jna:jna-platform:$jna_version"
}

configure([jvmJar, jvmIrJar]) {
    manifest {
        attributes "Premain-Class": "kotlinx.coroutines.debug.AgentPremain"
        attributes "Can-Redefine-Classes": "true"
    }
}

['jvm', 'jvmIr'].forEach { jvmCompilerType ->
    tasks.register("shadow${jvmCompilerType.capitalize()}Jar", com.github.jengelman.gradle.plugins.shadow.tasks.ShadowJar) {
        def jarTask = tasks.getByName("${jvmCompilerType}Jar")
        dependsOn(jarTask)
        def target = kotlin.targets[jvmCompilerType]
        archiveAppendix.set(jarTask.archiveAppendix)
        from target.compilations.main.output
        archiveClassifier.set(null)
        manifest.inheritFrom jarTask.manifest
        // Shadow only byte buddy, do not package kotlin stdlib
        configurations = [project.configurations.shadowDeps]
        relocate('net.bytebuddy', 'kotlinx.coroutines.repackaged.net.bytebuddy')
        exclude('META-INF/INDEX.LIST', 'META-INF/*.SF', 'META-INF/*.DSA', 'META-INF/*.RSA')
    }
}
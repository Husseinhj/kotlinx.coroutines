/*
 * Copyright 2016-2020 JetBrains s.r.o. Use of this source code is governed by the Apache 2.0 license.
 */

sourceCompatibility = 1.6
targetCompatibility = 1.6

def jvmCompilerAttr = Attribute.of('org.jetbrains.kotlin.jvm.compiler', String)

kotlin {
    jvm('jvm') {
        if (rootProject.ext.build_snapshot_train) {
            attributes.attribute(jvmCompilerAttr, 'jvm')
        }
    }
    if (rootProject.ext.build_snapshot_train) {
        jvm('jvmIr') {
            attributes.attribute(jvmCompilerAttr, 'jvmIr')
            compilations.all {
                kotlinOptions.useIR = true
            }
            // Do not publish JVM IR into repository
            mavenPublication { targetPublication ->
                tasks.withType(PublishToMavenRepository)
                        .matching { it.publication == targetPublication }
                        .all { onlyIf { false } }
            }
        }
    }
    sourceSets {
        jvmTest.dependencies {
            api "org.jetbrains.kotlin:kotlin-test:$kotlin_version"
            // Workaround to make addSuppressed work in tests
            api "org.jetbrains.kotlin:kotlin-reflect:$kotlin_version"
            api "org.jetbrains.kotlin:kotlin-stdlib-jdk7:$kotlin_version"
            api "org.jetbrains.kotlin:kotlin-test-junit:$kotlin_version"
            api "junit:junit:$junit_version"
        }
        if (rootProject.ext.build_snapshot_train) {
            jvmIrMain {
                dependsOn jvmMain
            }
            jvmIrTest {
                dependsOn jvmTest
            }
        }
    }
}

tasks.withType(Test) {
    testLogging {
        showStandardStreams = true
        events "passed", "failed"
    }
    def stressTest = project.properties['stressTest']
    if (stressTest != null) systemProperties['stressTest'] = stressTest
}
